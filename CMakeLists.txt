cmake_minimum_required(VERSION 2.8.11)
project(libosmscout C CXX)
set(CMAKE_ALLOW_LOOSE_LOOP_CONSTRUCTS ON)
set(CMAKE_CXX_STANDARD_REQUIRED ON) # build should fail when compiler don't support standard defined by CMAKE_CXX_STANDARD
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/" "$ENV{QTDIR}" "${QTDIR}" "$ENV{QTDIR}/lib/cmake" "${QTDIR}/lib/cmake")

# default build type
if(CMAKE_BUILD_TYPE STREQUAL "")
  set(CMAKE_BUILD_TYPE "RelWithDebInfo" CACHE STRING "Choose the type of build, options are: None (CMAKE_CXX_FLAGS or CMAKE_C_FLAGS used) Debug Release RelWithDebInfo MinSizeRel." FORCE)
endif()

option(BUILD_SHARED_LIBS "Shared libraries " ON)  # SHARED lib default
option(BUILD_WITH_OPENMP "Check for OpenMP support " ON)
option(BUILD_IMPORT_TOOL_FOR_DISTRIBUTION "Import tool for distribution " OFF)

# compiler configurations
if(WIN32)
  set(CMAKE_USE_RELATIVE_PATHS true)
  set(CMAKE_SUPPRESS_REGENERATION true)
endif()

if(APPLE AND NOT ANDROID)
  set(CMAKE_SIZEOF_VOID_P 4)
  set(CMAKE_XCODE_ATTRIBUTE_GCC_VERSION "com.apple.compilers.llvm.clang.1_0")
  set(CMAKE_XCODE_ATTRIBUTE_CLANG_CXX_LANGUAGE_STANDARD "c++11")
  set(CMAKE_XCODE_ATTRIBUTE_CLANG_CXX_LIBRARY "libc++")
endif ()

# CXX_STANDARD property is supported from cmake 3.1, we have to define -std with old cmake
if(NOT MSVC)
  if(${CMAKE_MAJOR_VERSION}.${CMAKE_MINOR_VERSION} LESS 3.1)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
  endif ()
endif()

include(CheckTypeSize)
CHECK_TYPE_SIZE("void*" OSMSCOUT_PTR_SIZE BUILTIN_TYPES_ONLY)
if(OSMSCOUT_PTR_SIZE EQUAL 8)
  set(OSMSCOUT_PLATFORM_X64 TRUE)
else ()
  set(OSMSCOUT_PLATFORM_X64 FALSE)
endif ()

include(CheckCXXCompilerFlag)
if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_COMPILER_IS_CLANGXX OR CMAKE_COMPILER_IS_GNUCC)
  set(OSMSCOUT_WARNING_FLAGS "-Wall -Winit-self -Wno-overloaded-virtual -Wcast-qual -Wwrite-strings -Wextra -Wno-long-long")
  if(EMSCRIPTEN)
    set(OSMSCOUT_WARNING_FLAGS "${OSMSCOUT_WARNING_FLAGS} -Wno-warn-absolute-paths")
  elseif(NOT APPLE)
    set(OSMSCOUT_WARNING_FLAGS "${OSMSCOUT_WARNING_FLAGS} -Wno-unused-but-set-parameter")
  endif()
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OSMSCOUT_WARNING_FLAGS}")
endif()

if(MSVC)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /fp:fast /wd4251")
  set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /Oi")
  if(CMAKE_CL_64)
	set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /bigobj")
	set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO} /bigobj")
  endif()
  if(MSVC_VERSION GREATER 1500 OR MSVC_VERSION EQUAL 1500)
    option(OSMSCOUT_BUILD_MSVC_MP "Enable build with multiple processes in Visual Studio" TRUE)
  else()
    set(OSMSCOUT_BUILD_MSVC_MP FALSE CACHE BOOL "Compiler option /MP requires at least Visual Studio 2008 (VS9) or newer" FORCE)
  endif()
  if(OSMSCOUT_BUILD_MSVC_MP)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /MP")
  endif()
  add_definitions(-D_CRT_SECURE_NO_DEPRECATE -D_CRT_NONSTDC_NO_DEPRECATE -DDLL_EXPORT)
endif()

if(MINGW)
  add_definitions(-D_WIN32_WINNT=0x0600)
  if(NOT OSMSCOUT_PLATFORM_X64)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -march=i686")
  endif ()
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fpermissive -fno-tree-slp-vectorize -Wno-error=cast-qual -Wno-unused-local-typedefs")
endif()

if((CMAKE_COMPILER_IS_GNUCXX OR CMAKE_COMPILER_IS_CLANGXX OR CMAKE_COMPILER_IS_GNUCC) AND NOT MINGW)
  add_definitions( -Wall -pedantic -fPIC )
  check_cxx_compiler_flag(-fvisibility=hidden OSMSCOUT_GCC_VISIBILITY)
  if(OSMSCOUT_GCC_VISIBILITY)
    execute_process(COMMAND ${CMAKE_CXX_COMPILER} -dumpversion OUTPUT_VARIABLE OSMSCOUT_GCC_VERSION)
    message(STATUS "Detected g++ ${OSMSCOUT_GCC_VERSION}")
    message(STATUS "Enabling GCC visibility flags")
    set(OSMSCOUT_GCC_VISIBILITY_FLAGS "-fvisibility=hidden")
    set(XCODE_ATTRIBUTE_GCC_SYMBOLS_PRIVATE_EXTERN "YES")
    string(TOLOWER "${CMAKE_BUILD_TYPE}" OSMSCOUT_BUILD_TYPE)
    if(OSMSCOUT_BUILD_TYPE STREQUAL "debug" AND OSMSCOUT_GCC_VERSION VERSION_LESS "4.2")
      message(STATUS "Skipping -fvisibility-inlines-hidden due to possible bug in g++ < 4.2")
    else()
      if(APPLE)
        message(STATUS "Skipping -fvisibility-inlines-hidden due to linker issues")
        set(XCODE_ATTRIBUTE_GCC_INLINES_ARE_PRIVATE_EXTERN[arch=x86_64] "YES")
      else()
        set(OSMSCOUT_VISIBILITY_FLAGS "${OSMSCOUT_GCC_VISIBILITY_FLAGS} -fvisibility-inlines-hidden")
        set(XCODE_ATTRIBUTE_GCC_INLINES_ARE_PRIVATE_EXTERN "YES")
      endif()
    endif()
  endif()
  if(OSMSCOUT_PLATFORM_X64 AND NOT APPLE)
    add_definitions(-fPIC)
  endif()
endif()

# prefer static libraries if making import tool
if(BUILD_IMPORT_TOOL_FOR_DISTRIBUTION AND (CMAKE_COMPILER_IS_GNUCXX OR CMAKE_COMPILER_IS_CLANGXX OR CMAKE_COMPILER_IS_GNUCC))
  SET(CMAKE_FIND_LIBRARY_SUFFIXES .a ${CMAKE_FIND_LIBRARY_SUFFIXES})
endif()

# postfix for debug builds
if(NOT APPLE)
  set(CMAKE_DEBUG_POSTFIX "d")
endif ()

# global variables
set(OSMSCOUT_BASE_DIR_SOURCE ${CMAKE_CURRENT_SOURCE_DIR})
set(OSMSCOUT_BASE_DIR_BUILD ${CMAKE_CURRENT_BINARY_DIR})
include_directories(${OSMSCOUT_BASE_DIR_BUILD}/include)

# feature detection (libraries, header, etc.)
include(features)

set(OSMSCOUT_BUILD_CORE OFF CACHE INTERNAL "" FORCE)
add_subdirectory(libosmscout)
if(LIBXML2_FOUND AND PROTOBUF_FOUND AND ZLIB_FOUND)
  set(OSMSCOUT_BUILD_IMPORT_CACHE ON)
else()
  set(OSMSCOUT_BUILD_IMPORT_CACHE OFF)
endif()
if(MSVC AND NOT (MSVC_VERSION GREATER 1800))
  set(OSMSCOUT_BUILD_IMPORT_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_IMPORT "Enable build of import library" ${OSMSCOUT_BUILD_IMPORT_CACHE})
if(OSMSCOUT_BUILD_IMPORT)
  add_subdirectory(libosmscout-import)
endif()
set(OSMSCOUT_BUILD_MAP OFF CACHE INTERNAL "" FORCE)
add_subdirectory(libosmscout-map)
if(LIBAGG_FOUND AND FREETYPE_FOUND AND OSMSCOUT_BUILD_MAP)
  set(OSMSCOUT_BUILD_MAP_AGG_CACHE ON)
else()
  set(OSMSCOUT_BUILD_MAP_AGG_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_MAP_AGG "Enable build of agg map drawing backend" ${OSMSCOUT_BUILD_MAP_AGG_CACHE})
if(OSMSCOUT_BUILD_MAP_AGG)
  add_subdirectory(libosmscout-map-agg)
endif()
if(CAIRO_FOUND AND OSMSCOUT_BUILD_MAP)
  set(OSMSCOUT_BUILD_MAP_CAIRO_CACHE ON)
else()
  set(OSMSCOUT_BUILD_MAP_CAIRO_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_MAP_CAIRO "Enable build of cairo map drawing backend" ${OSMSCOUT_BUILD_MAP_CAIRO_CACHE})
if(OSMSCOUT_BUILD_MAP_CAIRO)
  add_subdirectory(libosmscout-map-cairo)
endif()
if(OPENGL_FOUND AND GLUT_FOUND AND OSMSCOUT_BUILD_MAP)
  set(OSMSCOUT_BUILD_MAP_OPENGL_CACHE ON)
else()
  set(OSMSCOUT_BUILD_MAP_OPENGL_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_MAP_OPENGL "Enable build of OpenGL map drawing backend" ${OSMSCOUT_BUILD_MAP_OPENGL_CACHE})
if(OSMSCOUT_BUILD_MAP_OPENGL)
  add_subdirectory(libosmscout-map-opengl)
endif()
if(Direct2D_FOUND AND OSMSCOUT_BUILD_MAP AND MSVC)
  set(OSMSCOUT_BUILD_MAP_DIRECTX_CACHE ON)
else()
  set(OSMSCOUT_BUILD_MAP_DIRECTX_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_MAP_DIRECTX "Enable build of DirectX map drawing backend" ${OSMSCOUT_BUILD_MAP_DIRECTX_CACHE})
if(OSMSCOUT_BUILD_MAP_DIRECTX)
  add_subdirectory(libosmscout-map-directx)
endif()
if(Qt5Gui_FOUND AND OSMSCOUT_BUILD_MAP)
  set(OSMSCOUT_BUILD_MAP_QT_CACHE ON)
else()
  set(OSMSCOUT_BUILD_MAP_QT_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_MAP_QT "Enable build of Qt map drawing backend" ${OSMSCOUT_BUILD_MAP_QT_CACHE})
if(OSMSCOUT_BUILD_MAP_QT)
  add_subdirectory(libosmscout-map-qt)
endif()
if(Qt5Gui_FOUND AND OSMSCOUT_BUILD_MAP_QT)
  set(OSMSCOUT_BUILD_CLIENT_QT_CACHE ON)
else()
  set(OSMSCOUT_BUILD_CLIENT_QT_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_CLIENT_QT "Enable build of Qt client library" ${OSMSCOUT_BUILD_CLIENT_QT_CACHE})
if(OSMSCOUT_BUILD_CLIENT_QT)
  add_subdirectory(libosmscout-client-qt)
endif()
option(OSMSCOUT_BUILD_MAP_SVG "Enable build of SVG map drawing backend" ${OSMSCOUT_BUILD_MAP})
if(OSMSCOUT_BUILD_MAP_SVG)
  add_subdirectory(libosmscout-map-svg)
endif()
if(APPLE)
  option(OSMSCOUT_BUILD_MAP_IOSX "Enable build of iOS/OSX map drawing backend" ${OSMSCOUT_BUILD_MAP})
  if(OSMSCOUT_BUILD_MAP_IOSX)
    #ntbf
  endif()
else()
  set(OSMSCOUT_BUILD_MAP_IOSX OFF CACHE INTERNAL "" FORCE)
endif()
if(GPERFTOOLS_FOUND)
  set(GPERFTOOLS_USAGE ON)
else()
  set(GPERFTOOLS_USAGE OFF)
endif()

option(OSMSCOUT_BUILD_TOOL_IMPORT "Enable build of import applications" ${OSMSCOUT_BUILD_IMPORT})
if(OSMSCOUT_BUILD_TOOL_IMPORT)
  add_subdirectory(Import)
endif()

option(OSMSCOUT_BUILD_DEMOS "Enable build of demo applications" TRUE)
if(OSMSCOUT_BUILD_DEMOS)
  add_subdirectory(Demos)
endif()

option(OSMSCOUT_BUILD_TOOL_DUMPDATA "Enable build of dump tool" ${OSMSCOUT_BUILD_IMPORT})
if(OSMSCOUT_BUILD_TOOL_DUMPDATA)
  add_subdirectory(DumpData)
endif()

option(OSMSCOUT_BUILD_TOOL_OSMSCOUT2 "Enable build of OSMScout2 applications" ${OSMSCOUT_BUILD_CLIENT_QT})
if(OSMSCOUT_BUILD_TOOL_OSMSCOUT2)
  add_subdirectory(OSMScout2)
endif()

option(OSMSCOUT_BUILD_TOOL_STYLEEDITOR "Enable build of style editor applications" ${OSMSCOUT_BUILD_CLIENT_QT})
if(OSMSCOUT_BUILD_TOOL_STYLEEDITOR)
  add_subdirectory(StyleEditor)
endif()

option(OSMSCOUT_BUILD_TESTS "Enable build of test applications" ON)
if(OSMSCOUT_BUILD_TESTS)
  add_subdirectory(Tests)
endif()

if(DOXYGEN_FOUND)
  set(OSMSCOUT_BUILD_DOC_API_CACHE ON)
else()
  set(OSMSCOUT_BUILD_DOC_API_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_DOC_API "Enable generation of api documentation" ${OSMSCOUT_BUILD_DOC_API_CACHE})
if(OSMSCOUT_BUILD_DOC_API)
  add_subdirectory(Documentation)
endif()

if(SWIG_FOUND AND JNI_FOUND)
  set(OSMSCOUT_BUILD_BINDING_JAVA_CACHE ON)
else()
  set(OSMSCOUT_BUILD_BINDING_JAVA_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_BINDING_JAVA "Enable generation of java binding" ${OSMSCOUT_BUILD_BINDING_JAVA_CACHE})
if(SWIG_FOUND AND MSVC)
  set(OSMSCOUT_BUILD_BINDING_CSHARP_CACHE ON)
else()
  set(OSMSCOUT_BUILD_BINDING_CSHARP_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_BINDING_CSHARP "Enable generation of c# binding" ${OSMSCOUT_BUILD_BINDING_CSHARP_CACHE})
add_subdirectory(libosmscout-binding)

if(MATLAB_FOUND)
  set(OSMSCOUT_BUILD_EXTERN_MATLAB_CACHE ON)
else()
  set(OSMSCOUT_BUILD_EXTERN_MATLAB_CACHE OFF)
endif()
option(OSMSCOUT_BUILD_EXTERN_MATLAB "Enable generation of MATLAB mex function" ${OSMSCOUT_BUILD_EXTERN_MATLAB_CACHE})
add_subdirectory(libosmscout-extern)

# display build configuration
message(STATUS "libosmscout build configuration:")
message(STATUS "Shared libraries:                        ${BUILD_SHARED_LIBS}")
message(STATUS "core library:                            ${OSMSCOUT_BUILD_CORE}")
message(STATUS "import library:                          ${OSMSCOUT_BUILD_IMPORT}")
message(STATUS "map drawing interface:                   ${OSMSCOUT_BUILD_MAP}")
message(STATUS " - Agg map drawing backend:              ${OSMSCOUT_BUILD_MAP_AGG}")
message(STATUS " - Cairo map drawing backend:            ${OSMSCOUT_BUILD_MAP_CAIRO}")
message(STATUS " - OpenGL map drawing backend:           ${OSMSCOUT_BUILD_MAP_OPENGL}")
message(STATUS " - DirectX map drawing backend:          ${OSMSCOUT_BUILD_MAP_DIRECTX}")
message(STATUS " - Qt map drawing backend:               ${OSMSCOUT_BUILD_MAP_QT}")
message(STATUS " - SVG map drawing backend:              ${OSMSCOUT_BUILD_MAP_SVG}")
message(STATUS " - Map drawing backend for iOS and OS X: ${OSMSCOUT_BUILD_MAP_IOSX}")
message(STATUS "client libraries:")
message(STATUS " - Qt client library:                    ${OSMSCOUT_BUILD_CLIENT_QT}")
message(STATUS "tools:")
message(STATUS " - Import:                               ${OSMSCOUT_BUILD_TOOL_IMPORT}")
message(STATUS " - DumpData:                             ${OSMSCOUT_BUILD_TOOL_DUMPDATA}")
message(STATUS " - OSMScout2:                            ${OSMSCOUT_BUILD_TOOL_OSMSCOUT2}")
message(STATUS " - StyleEditor:                          ${OSMSCOUT_BUILD_TOOL_STYLEEDITOR}")
message(STATUS "tests:                                   ${OSMSCOUT_BUILD_TESTS}")
message(STATUS "demos:                                   ${OSMSCOUT_BUILD_DEMOS}")
message(STATUS " - heap profiler (Gperftools)            ${GPERFTOOLS_USAGE}")
message(STATUS "bindings:")
message(STATUS " - Java binding:                         ${OSMSCOUT_BUILD_BINDING_JAVA}")
message(STATUS " - C# binding:                           ${OSMSCOUT_BUILD_BINDING_CSHARP}")
message(STATUS "external interfaces:")
message(STATUS " - MATLAB mex function:                  ${OSMSCOUT_BUILD_EXTERN_MATLAB}")
message(STATUS "documentation:                           ${OSMSCOUT_BUILD_DOC_API}")

install(DIRECTORY ${CMAKE_SOURCE_DIR}/stylesheets DESTINATION share)
